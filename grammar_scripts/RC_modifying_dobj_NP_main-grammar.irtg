{## Subsets of main grammar that only generate
RC modification in object position
object extracted:
    - main theme --> RC theme  # Emma saw the boy that the guy found _
    - main theme --> RC dat theme  # Emma saw the cake that the boy gave _ to the cat
    - main dat theme --> RC theme  # Emma gave the girl the cake that the guy found _
    - main dat theme --> RC dat theme # Emma gave the cake that the boy sold _ to Liam to the cat
    - main passive dat theme * RC # Emma was given a cake that Liam baked _
subject extracted:
    - main theme --> RC agent  # Emma saw the boy that found the cat
    - main theme/dat theme --> RC unaccusative theme # Emma saw the boy that froze
    - main theme/dat theme --> RC passive theme  # Emma saw the boy that was given a cake
    - main passive dat theme * RC # Emma was given a cake that burned _
embedded RC allowed: The student said that Emma saw the boy that the guy found [10%]
##}


S! -> S_obj_modif_main_theme_E{{ cnt.next() }}(NP_animate_nsubj_main, V_main, NP_animate_dobj_RC_modified) [0.3]
[english] *(*(?1, ?2),?3)
[semantics] theme(pre_agent(?2, ?1),?3)

S! -> S_obj_modif_main_theme_E{{ cnt.next() }}(NP_animate_nsubj_main, V_main, NP_inanimate_dobj_RC_modified) [0.3]
[english] *(*(?1, ?2),?3)
[semantics] theme(pre_agent(?2, ?1),?3)

S! -> S_obj_modif_main_dat_theme_E{{ cnt.next() }}(NP_animate_nsubj_main, V_dat, NP_animate_iobj, NP_inanimate_dobj_RC_modified) [0.1]
[english] *(*(*(?1, ?2), ?3), ?4)
[semantics] theme(recipient(agent(?2, ?1),?3),?4)

S! -> S_obj_modif_main_dat_theme_E{{ cnt.next() }}(NP_animate_nsubj_main, V_dat, NP_inanimate_dobj_RC_modified, PP_iobj) [0.1]
[english] *(*(*(?1, ?2), ?3), ?4)
[semantics] recipient(theme(agent(?2, ?1),?3),?4)

S! -> S_obj_modif_main_passive_dat_theme_E{{ cnt.next() }}(NP_animate_nsubj_main, AUX, V_dat_pp, NP_inanimate_dobj_RC_modified) [0.1]
[english] *(*(*(?1, ?2), ?3), ?4)
[semantics] theme(recipient(?3, ?1),?4)
{# dobj RC can end with by phrase, ambiguous attachement
S! -> S_obj_modif_main_passive_dat_theme_E{{ cnt.next() }}(NP_animate_nsubj_main, AUX, V_dat_pp, NP_inanimate_dobj_RC_modified, BY, NP_animate_nsubj) [0.05]
[english] *(*(*(*(?1, ?2), ?3), ?4),*(?5,?6))
[semantics] agent(theme(recipient(?3, ?1),?4),?6)#}

S! -> r{{ cnt.next() }}(NP_animate_nsubj, VP_cp) [0.1]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

VP_cp -> I_cp_embedded_RC_F{{ cnt.next() }}(V_cp_taking, C, S!) [1]
[english] *(?1, *(?2, ?3))
[semantics] ccomp(?1, ?3)

V_main -> r{{ cnt.next() }}(V_unacc) [0.33]
[english] ?1
[semantics] ?1

V_main -> r{{ cnt.next() }}(V_trans_omissible) [0.33]
[english] ?1
[semantics] ?1

V_main -> r{{ cnt.next() }}(V_trans_not_omissible) [0.34]
[english] ?1
[semantics] ?1

{# object extracted RCs #}
NP_animate_dobj_RC_modified -> S_RC_theme_dobj_extracted_E{{ cnt.next() }}(NP_animate_dobj_RC, Rel_pron, VP_RC_theme) [0.5]
[english] *(*(?1, ?2),?3)
[semantics] 'nmod<that>'(?1, theme(?3,?2))

NP_inanimate_dobj_RC_modified -> S_RC_theme_dobj_extracted_E{{ cnt.next() }}(NP_inanimate_dobj, Rel_pron, VP_RC_theme) [0.3]
[english] *(*(?1, ?2),?3)
[semantics] 'nmod<that>'(?1, theme(?3,?2))

NP_inanimate_dobj_RC_modified -> S_RC_dat_theme_dobj_extracted_E{{ cnt.next() }}(NP_inanimate_dobj, Rel_pron, VP_dat_RC_theme) [0.1]
[english] *(*(?1, ?2),?3)
[semantics] 'nmod<that>'(?1, theme(?3,?2))

VP_RC_theme -> r{{ cnt.next() }}(NP_animate_nsubj, V_unacc) [0.33]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

VP_RC_theme -> r{{ cnt.next() }}(NP_animate_nsubj, V_trans_omissible) [0.33]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

VP_RC_theme -> r{{ cnt.next() }}(NP_animate_nsubj, V_trans_not_omissible) [0.34]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

NP_inanimate_dobj_RC_modified -> S_RC_dat_theme_dobj_extracted_E{{ cnt.next() }}(NP_inanimate_dobj, Rel_pron, NP_animate_nsubj, V_dat, PP_iobj) [0.1]
[english] *(*(*(?1, ?2),?3),*(?4,?5))
[semantics] 'nmod<that>'(?1, recipient(theme(agent(?4, ?3),?2),?5))
{# replace the following rule with the previous: handle the extracted theme ordering issue (dat verbs)
"cake that Emma sold __ to Liam ..."
VP_dat_RC_theme -> r{{ cnt.next() }}(NP_animate_nsubj, V_dat, PP_iobj) [0.5]
[english] *(*(?1, ?2), ?3)
[semantics] recipient(pre_agent(?2, ?1),?3)
#}

VP_dat_RC_theme -> r{{ cnt.next() }}(NP_animate_nsubj, V_dat, NP_animate_iobj) [1]
[english] *(*(?1, ?2), ?3)
[semantics] recipient(agent(?2, ?1),?3)

NP_animate_nsubj_main -> r{{ cnt.next() }}(N_prop_nsubj) [0.5]
[english] ?1
[semantics] ?1

NP_animate_nsubj_main -> r{{ cnt.next() }}(Det, N_common_animate_nsubj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)


{# subject extracted RCs #}
NP_animate_dobj_RC_modified -> S_RC_agent_subj_extracted_E{{ cnt.next() }}(NP_animate_dobj_RC, Rel_pron, VP_RC_agent) [0.35]
[english] *(*(?1, ?2),?3)
[semantics] 'nmod<that>'(?1, pre_agent(?3,?2))

NP_animate_dobj_RC_modified -> S_RC_unacc_theme_subj_extracted_E{{ cnt.next() }}(NP_animate_dobj_RC, Rel_pron, VP_RC_unacc_theme) [0.05]
[english] *(*(?1, ?2),?3)
[semantics] 'nmod<that>'(?1, theme(?3,?2))

NP_animate_dobj_RC_modified -> S_RC_pass_recipient_subj_extracted_E{{ cnt.next() }}(NP_animate_dobj_RC, Rel_pron, VP_RC_pass_recipient) [0.1]
[english] *(*(?1, ?2),?3)
[semantics] 'nmod<that>'(?1, pre_recipient(?3,?2))

NP_inanimate_dobj_RC_modified -> S_RC_unacc_theme_subj_extracted_E{{ cnt.next() }}(NP_inanimate_dobj, Rel_pron, VP_RC_unacc_theme) [0.05]
[english] *(*(?1, ?2),?3)
[semantics] 'nmod<that>'(?1, theme(?3,?2))

{# including V_dat passive forms #}
NP_inanimate_dobj_RC_modified -> S_RC_pass_theme_subj_extracted_E{{ cnt.next() }}(NP_inanimate_dobj, Rel_pron, VP_RC_pass_theme) [0.45]
[english] *(*(?1, ?2),?3)
[semantics] 'nmod<that>'(?1, pre_theme(?3,?2))

VP_RC_unacc_theme -> r{{ cnt.next() }}(V_unacc) [1]
[english] ?1
[semantics] ?1

VP_RC_pass_recipient -> r{{ cnt.next() }}(AUX, V_dat_pp, NP_inanimate_dobj) [0.5]
[english] *(*(?1, ?2),?3)
[semantics] theme(?2,?3)

VP_RC_pass_recipient -> r{{ cnt.next() }}(AUX, V_dat_pp, NP_inanimate_dobj, BY, NP_animate_nsubj) [0.5]
[english] *(*(*(?1, ?2),?3),*(?4,?5))
[semantics] agent(theme(?2,?3),?5)

{# passive RCs #}
VP_RC_pass_theme -> r{{ cnt.next() }}(AUX,V_trans_not_omissible_pp) [0.125]
[english] *(?1, ?2)
[semantics] ?2

VP_RC_pass_theme -> r{{ cnt.next() }}(AUX,V_trans_not_omissible_pp, BY,NP_animate_nsubj) [0.125]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_RC_pass_theme -> r{{ cnt.next() }}(AUX, V_trans_omissible_pp) [0.125]
[english] *(?1, ?2)
[semantics] ?2

VP_RC_pass_theme -> r{{ cnt.next() }}(AUX, V_trans_omissible_pp, BY,NP_animate_nsubj) [0.125]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_RC_pass_theme -> r{{ cnt.next() }}(AUX, V_unacc_pp) [0.125]
[english] *(?1, ?2)
[semantics] ?2

VP_RC_pass_theme -> r{{ cnt.next() }}(AUX, V_unacc_pp, BY,NP_animate_nsubj) [0.125]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_RC_pass_theme -> r{{ cnt.next() }}(AUX, V_dat_pp, PP_iobj) [0.125]
[english] *(*(?1, ?2),?3)
[semantics] recipient(?2,?3)

VP_RC_pass_theme -> r{{ cnt.next() }}(AUX, V_dat_pp, PP_iobj, BY,NP_animate_nsubj) [0.125]
[english] *(*(*(?1, ?2),?3),*(?4,?5))
[semantics] agent(recipient(?2,?3),?5)


VP_RC_agent -> r{{ cnt.next() }}(V_unerg) [0.10525]
[english] ?1
[semantics] ?1

VP_RC_agent -> r{{ cnt.next() }}(V_unacc, NP_dobj) [0.10525]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_RC_agent -> r{{ cnt.next() }}(V_trans_omissible) [0.10525]
[english] ?1
[semantics] ?1

VP_RC_agent -> r{{ cnt.next() }}(V_trans_omissible, NP_dobj) [0.10525]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_RC_agent -> r{{ cnt.next() }}(V_trans_not_omissible, NP_dobj) [0.10525]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_RC_agent -> I_RC_control_V_F{{ cnt.next() }}(V_inf_taking, INF, V_inf)[0.10525]
[english] *(?1, *(?2, ?3))
[semantics] xcomp(?1, ?3)

VP_RC_agent -> r{{ cnt.next() }}(V_dat, NP_inanimate_dobj, PP_iobj) [0.10525]
[english] *(*(?1, ?2), ?3)
[semantics] recipient(theme(?1, ?2), ?3)

VP_RC_agent -> r{{ cnt.next() }}(V_dat, NP_animate_iobj, NP_inanimate_dobj) [0.10525]
[english] *(*(?1, ?2), ?3)
[semantics] theme(recipient(?1, ?2), ?3)

{# allows to generate CP instances in the case of RC modifying object:
 - restrictions: no embedded RC modifier
 e.g. the boy found the guy that said that Emma froze a cat (that slept)
 - restrictions: no embedded cp
 e.g. the boy found the guy that said that (Emma believed that) the cat slept.
 cases allowed:
 - the boy found the guy [that _ said that Emma froze a cat]
  #}
VP_RC_agent -> I_RC_cp_V_F{{ cnt.next() }}(V_cp_taking, C, C_internal) [0.158]
[english] *(?1, *(?2, ?3))
[semantics] ccomp(?1, ?3)

NP_animate_nsubj -> r{{ cnt.next() }}(N_prop_nsubj) [0.5]
[english] ?1
[semantics] ?1

NP_animate_nsubj -> r{{ cnt.next() }}(Det, N_common_animate_nsubj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

PP_iobj -> r{{ cnt.next() }} (P_iobj, NP_animate_iobj)[1]
[english] *(?1, ?2)
[semantics] pre_case(?2, ?1)

NP_animate_iobj -> r{{ cnt.next() }}(Det, N_common_animate_iobj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_iobj -> r{{ cnt.next() }}(N_prop_iobj) [0.5]
[english] ?1
[semantics] ?1

NP_animate_dobj_RC -> r{{ cnt.next() }}(Det, N_common_animate_dobj) [1]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_inanimate_dobj -> r{{ cnt.next() }}(Det, N_common_inanimate_dobj) [1]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

C_internal -> r{{ cnt.next() }}(NP_animate_nsubj, VP_external) [0.49]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

C_internal -> r{{ cnt.next() }}(VP_internal) [0.06]
[english] ?1
[semantics] ?1

C_internal -> r{{ cnt.next() }}(NP_inanimate_nsubjpass, VP_passive) [0.375]
[english] *(?1, ?2)
[semantics] pre_theme(?2, ?1)

C_internal -> r{{ cnt.next() }}(NP_animate_nsubjpass, VP_passive_dat) [0.075]
[english] *(?1, ?2)
[semantics] pre_recipient(?2, ?1)

{# embedded CP inside RC modifier not allowed
VP_external -> r{{ cnt.next() }}(V_cp_taking, C, C_internal) [0.158]
[english] *(?1, *(?2, ?3))
[semantics] ccomp(?1, ?3)
#}
VP_external -> r{{ cnt.next() }}(V_unerg) [0.125]
[english] ?1
[semantics] ?1

VP_external -> r{{ cnt.next() }}(V_unacc, NP_dobj) [0.125]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_external -> r{{ cnt.next() }}(V_trans_omissible) [0.125]
[english] ?1
[semantics] ?1

VP_external -> r{{ cnt.next() }}(V_trans_omissible, NP_dobj) [0.125]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_external -> r{{ cnt.next() }}(V_trans_not_omissible, NP_dobj) [0.125]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_external -> r{{ cnt.next() }}(V_inf_taking, INF, V_inf)[0.125]
[english] *(?1, *(?2, ?3))
[semantics] xcomp(?1, ?3)

VP_external -> r{{ cnt.next() }}(V_dat, NP_inanimate_dobj, PP_iobj) [0.125]
[english] *(*(?1, ?2), ?3)
[semantics] recipient(theme(?1, ?2), ?3)

VP_external -> r{{ cnt.next() }}(V_dat, NP_animate_iobj, NP_inanimate_dobj) [0.125]
[english] *(*(?1, ?2), ?3)
[semantics] theme(recipient(?1, ?2), ?3)

VP_internal -> r{{ cnt.next() }}(NP_unacc_subj, V_unacc) [1]
[english] *(?1, ?2)
[semantics] pre_theme(?2, ?1)


{## Noun phrases ##}

NP_dobj -> r{{ cnt.next() }}(NP_inanimate_dobj) [0.5]
[english] ?1
[semantics] ?1

NP_dobj -> r{{ cnt.next() }}(NP_animate_dobj) [0.5]
[english] ?1
[semantics] ?1

NP_unacc_subj  -> r{{ cnt.next() }}(Det, N_common_animate_dobj) [1]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)


NP_animate_dobj -> r{{ cnt.next() }}(Det, N_common_animate_dobj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_dobj -> r{{ cnt.next() }}(N_prop_dobj) [0.5]
[english] ?1
[semantics] ?1


NP_animate_nsubjpass -> r{{ cnt.next() }}(Det, N_common_animate_nsubjpass) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_nsubjpass -> r{{ cnt.next() }}(N_prop_nsubjpass) [0.5]
[english] ?1
[semantics] ?1

NP_inanimate_nsubjpass -> r{{ cnt.next() }}(Det, N_common_inanimate_nsubjpass) [1.0]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

PP_iobj -> r{{ cnt.next() }} (P_iobj,NP_animate_iobj)[1]
[english] *(?1, ?2)
[semantics] pre_case(?2, ?1)

VP_passive -> r{{ cnt.next() }}(AUX,V_trans_not_omissible_pp) [0.125]
[english] *(?1, ?2)
[semantics] ?2

VP_passive -> r{{ cnt.next() }}(AUX,V_trans_not_omissible_pp, BY,NP_animate_nsubj) [0.125]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_passive -> r{{ cnt.next() }}(AUX, V_trans_omissible_pp) [0.125]
[english] *(?1, ?2)
[semantics] ?2

VP_passive -> r{{ cnt.next() }}(AUX, V_trans_omissible_pp, BY,NP_animate_nsubj) [0.125]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_passive -> r{{ cnt.next() }}(AUX,V_unacc_pp) [0.125]
[english] *(?1, ?2)
[semantics] ?2

VP_passive -> r{{ cnt.next() }}(AUX, V_unacc_pp, BY,NP_animate_nsubj) [0.125]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_passive -> r{{ cnt.next() }}(AUX, V_dat_pp, PP_iobj) [0.125]
[english] *(*(?1, ?2),?3)
[semantics] recipient(?2,?3)

VP_passive -> r{{ cnt.next() }}(AUX, V_dat_pp, PP_iobj, BY,NP_animate_nsubj) [0.125]
[english] *(*(*(?1, ?2),?3),*(?4,?5))
[semantics] agent(recipient(?2,?3),?5)

VP_passive_dat -> r{{ cnt.next() }}(AUX, V_dat_pp, NP_inanimate_dobj) [0.5]
[english] *(*(?1, ?2),?3)
[semantics] theme(?2,?3)

VP_passive_dat -> r{{ cnt.next() }}(AUX, V_dat_pp, NP_inanimate_dobj, BY,NP_animate_nsubj) [0.5]
[english] *(*(*(?1, ?2),?3),*(?4,?5))
[semantics] agent(theme(?2,?3),?5)