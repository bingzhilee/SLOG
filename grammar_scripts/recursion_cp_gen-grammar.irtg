{# From Najoung's grammar: grammar with high CP probability, for CP recursion depth 3,5-12 #}

{## Sentences and verb phrases ##}

S! -> r{{ cnt.next() }}(NP_animate_nsubj, VP_external) [0.01]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

S! -> r{{ cnt.next() }}(VP_internal) [0.01]
[english] ?1
[semantics] ?1

S! -> r{{ cnt.next() }}(NP_inanimate_nsubjpass, VP_passive) [0.01]
[english] *(?1, ?2)
[semantics] pre_theme(?2, ?1)

S! -> r{{ cnt.next() }}(NP_animate_nsubjpass, VP_passive_dat) [0.01]
[english] *(?1, ?2)
[semantics] pre_recipient(?2, ?1)

S! -> r{{ cnt.next() }}(NP_animate_nsubj, VP_CP) [0.96]
[english] *(?1, ?2)
[semantics] pre_agent(?2, ?1)

VP_CP -> S_embedded_cp_E{{ cnt.next() }}(V_cp_taking, C, S!) [1]
[english] *(?1, *(?2, ?3))
[semantics] ccomp(?1, ?3)

VP_external -> r{{ cnt.next() }}(V_unerg) [0.125]
[english] ?1
[semantics] ?1

VP_external -> r{{ cnt.next() }}(V_unacc, NP_dobj) [0.125]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_external -> r{{ cnt.next() }}(V_trans_omissible) [0.125]
[english] ?1
[semantics] ?1

VP_external -> r{{ cnt.next() }}(V_trans_omissible, NP_dobj) [0.125]
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_external -> r{{ cnt.next() }}(V_trans_not_omissible, NP_dobj) [0.125] 
[english] *(?1, ?2)
[semantics] theme(?1, ?2)

VP_external -> r{{ cnt.next() }}(V_inf_taking, INF, V_inf)[0.125]
[english] *(?1, *(?2, ?3))
[semantics] xcomp(?1, ?3)

VP_external -> r{{ cnt.next() }}(V_dat, NP_inanimate_dobj, PP_iobj) [0.125] 
[english] *(*(?1, ?2), ?3)
[semantics] recipient(theme(?1, ?2), ?3)

VP_external -> r{{ cnt.next() }}(V_dat, NP_animate_iobj, NP_inanimate_dobj) [0.125] 
[english] *(*(?1, ?2), ?3)
[semantics] theme(recipient(?1, ?2), ?3)

VP_internal -> r{{ cnt.next() }}(NP_unacc_subj, V_unacc) [1]
[english] *(?1, ?2)
[semantics] pre_theme(?2, ?1)

VP_passive -> r{{ cnt.next() }}(AUX,V_trans_not_omissible_pp) [0.125]
[english] *(?1, ?2)
[semantics] ?2

VP_passive -> r{{ cnt.next() }}(AUX,V_trans_not_omissible_pp, BY,NP_animate_nsubj) [0.125]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_passive -> r{{ cnt.next() }}(AUX, V_trans_omissible_pp) [0.125]
[english] *(?1, ?2)
[semantics] ?2

VP_passive -> r{{ cnt.next() }}(AUX, V_trans_omissible_pp, BY,NP_animate_nsubj) [0.125]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_passive -> r{{ cnt.next() }}(AUX,V_unacc_pp) [0.125]
[english] *(?1, ?2)
[semantics] ?2

VP_passive -> r{{ cnt.next() }}(AUX, V_unacc_pp, BY,NP_animate_nsubj) [0.125]
[english] *(*(?1, ?2),*(?3,?4))
[semantics] agent(?2,?4)

VP_passive -> r{{ cnt.next() }}(AUX, V_dat_pp, PP_iobj) [0.125]
[english] *(*(?1, ?2),?3)
[semantics] recipient(?2,?3)

VP_passive -> r{{ cnt.next() }}(AUX, V_dat_pp, PP_iobj, BY,NP_animate_nsubj) [0.125]
[english] *(*(*(?1, ?2),?3),*(?4,?5))
[semantics] agent(recipient(?2,?3),?5)

VP_passive_dat -> r{{ cnt.next() }}(AUX, V_dat_pp, NP_inanimate_dobj) [0.5]
[english] *(*(?1, ?2),?3)
[semantics] theme(?2,?3)

VP_passive_dat -> r{{ cnt.next() }}(AUX, V_dat_pp, NP_inanimate_dobj, BY,NP_animate_nsubj) [0.5]
[english] *(*(*(?1, ?2),?3),*(?4,?5))
[semantics] agent(theme(?2,?3),?5)


{## Noun phrases ##}

NP_dobj -> r{{ cnt.next() }}(NP_inanimate_dobj) [0.5]
[english] ?1
[semantics] ?1

NP_dobj -> r{{ cnt.next() }}(NP_animate_dobj) [0.5]
[english] ?1
[semantics] ?1

NP_unacc_subj -> r{{ cnt.next() }}(NP_inanimate_dobj_noPP) [0.5]
[english] ?1
[semantics] ?1

NP_unacc_subj -> r{{ cnt.next() }}(NP_animate_dobj_noPP) [0.5]
[english] ?1
[semantics] ?1



NP_animate_dobj_noPP -> r{{ cnt.next() }}(Det, N_common_animate_dobj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_dobj_noPP -> r{{ cnt.next() }}(N_prop_dobj) [0.5]
[english] ?1
[semantics] ?1



NP_animate_dobj -> r{{ cnt.next() }}(Det, N_common_animate_dobj) [0.25]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_dobj -> r{{ cnt.next() }}(Det, N_common_animate_dobj, PP_loc) [0.25]
[english] *(*(?1, ?2), ?3)
[semantics] nmod(pre_det(?2, ?1), ?3)

NP_animate_dobj -> r{{ cnt.next() }}(N_prop_dobj) [0.5]
[english] ?1
[semantics] ?1

NP_animate_iobj -> r{{ cnt.next() }}(Det, N_common_animate_iobj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_iobj -> r{{ cnt.next() }}(N_prop_iobj) [0.5]
[english] ?1
[semantics] ?1



NP_animate_nsubj -> r{{ cnt.next() }}(Det, N_common_animate_nsubj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_nsubj -> r{{ cnt.next() }}(N_prop_nsubj) [0.5]
[english] ?1
[semantics] ?1


NP_animate_nsubjpass -> r{{ cnt.next() }}(Det, N_common_animate_nsubjpass) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_animate_nsubjpass -> r{{ cnt.next() }}(N_prop_nsubjpass) [0.5]
[english] ?1
[semantics] ?1


NP_inanimate_dobj -> r{{ cnt.next() }}(Det, N_common_inanimate_dobj) [0.5]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_inanimate_dobj -> r{{ cnt.next() }}(Det, N_common_inanimate_dobj, PP_loc) [0.5]
[english] *(*(?1, ?2), ?3)
[semantics] nmod(pre_det(?2, ?1), ?3)

NP_inanimate_dobj_noPP -> r{{ cnt.next() }}(Det, N_common_inanimate_dobj) [1.0]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_inanimate_nsubjpass -> r{{ cnt.next() }}(Det, N_common_inanimate_nsubjpass) [1.0]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_on -> r{{ cnt.next() }}(Det, N_on, PP_loc) [0.1]
[english] *(*(?1, ?2), ?3)
[semantics] nmod(pre_det(?2, ?1), ?3)

NP_on -> r{{ cnt.next() }}(Det, N_on) [0.9]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)


NP_in -> r{{ cnt.next() }}(Det, N_in, PP_loc) [0.1]
[english] *(*(?1, ?2), ?3)
[semantics] nmod(pre_det(?2, ?1), ?3)

NP_in -> r{{ cnt.next() }}(Det, N_in) [0.9]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)

NP_beside -> r{{ cnt.next() }}(Det, N_beside, PP_loc) [0.1]
[english] *(*(?1, ?2), ?3)
[semantics] nmod(pre_det(?2, ?1), ?3)

NP_beside -> r{{ cnt.next() }}(Det, N_beside) [0.9]
[english] *(?1, ?2)
[semantics] pre_det(?2, ?1)


{## Prepositional Phrases ##}

PP_loc -> r{{ cnt.next() }}(P_on, NP_on) [0.333]
[english] *(?1, ?2)
[semantics] pre_case(?2, ?1)

PP_loc -> r{{ cnt.next() }}(P_in, NP_in) [0.333]
[english] *(?1, ?2)
[semantics] pre_case(?2, ?1)

PP_loc -> r{{ cnt.next() }}(P_beside, NP_beside) [0.334]
[english] *(?1, ?2)
[semantics] pre_case(?2, ?1)

PP_iobj -> r{{ cnt.next() }} (P_iobj,NP_animate_iobj)[1]
[english] *(?1, ?2)
[semantics] pre_case(?2, ?1)

